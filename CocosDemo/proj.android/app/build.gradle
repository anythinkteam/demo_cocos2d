import org.gradle.internal.os.OperatingSystem

apply plugin: 'com.android.application'

android {
    compileSdkVersion PROP_COMPILE_SDK_VERSION.toInteger()

    defaultConfig {
        applicationId "com.superapp.filemanager"
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        externalNativeBuild {
            if (PROP_BUILD_TYPE == 'ndk-build') {
                ndkBuild {
                    targets 'MyGame'
                    arguments 'NDK_TOOLCHAIN_VERSION=clang'
                    arguments '-j' + Runtime.runtime.availableProcessors()

                    def module_paths = [project.file("../../cocos2d").absolutePath,
                                        project.file("../../cocos2d/cocos").absolutePath,
                                        project.file("../../cocos2d/external").absolutePath]
                    if (OperatingSystem.current().isWindows()) {
                        module_paths = module_paths.collect {it.replaceAll('\\\\', '/')}
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    }
                    else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }
                }
            }
            else if (PROP_BUILD_TYPE == 'cmake') {
                cmake {
                    targets 'MyGame'
                    arguments "-DCMAKE_FIND_ROOT_PATH=", "-DANDROID_STL=c++_static", "-DANDROID_TOOLCHAIN=clang", "-DANDROID_ARM_NEON=TRUE"
                    cppFlags "-frtti -fexceptions -fsigned-char"
                }
            }
        }

        ndk {
            abiFilters = []
            abiFilters.addAll(PROP_APP_ABI.split(':').collect{it as String})
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "../../Resources"
    }

    externalNativeBuild {
        if (PROP_BUILD_TYPE == 'ndk-build') {
            ndkBuild {
                path "jni/Android.mk"
            }
        }
        else if (PROP_BUILD_TYPE == 'cmake') {
            cmake {
                path "../../CMakeLists.txt"
            }
        }
    }

    signingConfigs {

       release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                }
            }
        }

        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                }
            }
        }
    }
}

android.applicationVariants.all { variant ->
    // delete previous files first
    delete "${buildDir}/intermediates/assets/${variant.dirName}"

    variant.mergeAssetsProvider.get().doLast {
        copy {
            from "${buildDir}/../../../Resources"
            into "${buildDir}/intermediates/assets/${variant.dirName}"
            exclude "**/*.gz"
        }
    }
}

repositories{
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':libcocos2dx')

    implementation fileTree(dir: 'libs/aar_uparpu', include: ['*.aar','*.jar']) //TopOn的基础包必须导入
    implementation fileTree(dir: 'libs/aar_plugin', include: ['*.aar','*.jar']) //插件包必须导入
    implementation fileTree(dir: 'libs/aar_gms_service', include: ['*.aar','*.jar']) //海外平台或者Admob必须导入

    implementation fileTree(dir: 'libs/aar_adcolony', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_admob', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_applovin', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_appnext', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_baidu', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_chartboost', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_facebook', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_flurry', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_gdt', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_inmobi', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_ironsource', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_ks', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_ksyun', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_luomi', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_maio', include: ['*.aar','*.jar'])
//    implementation fileTree(dir: 'libs/aar_mintegral_china', include: ['*.aar','*.jar']) //MTG国内版本
    implementation fileTree(dir: 'libs/aar_mintegral_international', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_mopub', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_nend', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_oneway', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_startapp', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_superawesome', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_tapjoy', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_toutiao', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_uniplay', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_unityads', include: ['*.aar','*.jar'])
    implementation fileTree(dir: 'libs/aar_vungle', include: ['*.aar','*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'

}
